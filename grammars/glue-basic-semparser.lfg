GLUE   BASIC   CONFIG (1.0)  
  ROOTCAT   CP.  
  FILES  .
  LEXENTRIES   (ENGLISH BASIC).  
  RULES   (ENGLISH BASIC).
  TEMPLATES (ENGLISH BASIC) (GLUE BASIC).
  GOVERNABLERELATIONS    SUBJ OBJ OBJ?+ OBL?+ COMP XCOMP.
  SEMANTICFUNCTIONS    ADJUNCT TOPIC FOCUS.  
  NONDISTRIBUTIVES    NUM PERS.
  EPSILON   e.  
  CHARACTERENCODING utf-8.
OPTIMALITYORDER NOGOOD.

----

"AVM representations of meaning constructors appear as members of the
GLUE set at f-structure.  This grammar differs from glue-basic.lfg in
that meanings are represented as typed terms of the lambda calculus.
To use this grammar, you must set the value for the variable semParser
to 1 in your xlerc file."

ENGLISH   BASIC   RULES (1.0)

   CP --> C'.

   C' --> (C) 
 	  IP.

   C =   Comp.

   IP --> (NP: (^ SUBJ) = !) 
 	  I'.

   I' --> (I)
 	  VP.

   I = Aux.

   VP --> V'.

   V' --> (V)
 	  (NP: (^ OBJ) = !)
 	  (NP: (^ OBJ-TH) = !)
 	  (CP: (^ COMP) = !).

   NP --> (Det)
 	  N'.

   N' --> (Adj: ! $ (^ ADJ))
 	  N.

----

ENGLISH   BASIC   LEXICON (1.0)

"Complementizer"

  that 	  Comp * (^ COMPFORM) = 'that'.

"Nouns"

  David   N * @(PROPERNOUN David).

  Chris   N * @(PROPERNOUN Chris).

  Tracy   N * @(PROPERNOUN Tracy).

  Kim   N * @(PROPERNOUN Kim).

  man 	  N * @(NOUN0 man).

  woman   N * @(NOUN0 woman).

  book 	  N * @(NOUN0 book).

  person  N * @(NOUN0 person).

  everyone N * @(QUANT everyone every person).

"Adjectives"

  interesting Adj * @(ADJ interesting).

"Determiners"

  the 	  Det * @(SPEC the).

  a 	  Det * @(SPEC a).

  every   Det * @(SPEC every).
  

"Verbs"

  yawned  V * @(VERB-SUBJ yawn)
	      @VPAST.

  smiled  V * @(VERB-SUBJ smile)
	      @VPAST.

  saw 	  V * @(VERB-SUBJ-OBJ see)
	      @VPAST.

  thought V * @(VERB-SUBJ-COMP think)
	      @COMP-TENSE
	      @VPAST.

  gave 	  V * @(VERB-SUBJ-OBJ-OBJTH give)
	      @VPAST.

----

ENGLISH   BASIC   TEMPLATES (1.0)

"Nouns"

   PROPERNOUN(P) = (^ PRED) = 'P'
	  @(GLUE-PROPERNOUN P).

   NOUN0(P) = (^ PRED) = 'P'
	  @(GLUE-NOUN0 P).

"Quantifiers like 'everyone' call the template for quantificational 
determiners and the template for common nouns."

   QUANT(P Q N) = (^ PRED) = 'P'
          @(GLUE-QUANT-MC Q)
          @(GLUE-NOUN0-MC N).

"Adjectives"

   ADJ(P) = (^ PRED) = 'P'
	  @(GLUE-ADJ0 P).

"Determiners"

   SPEC(Q) = (^ SPEC PRED) = 'Q'
	  @(GLUE-QUANT-DET Q).

"Verbs"

   VERB-SUBJ(P) =
	  (^ PRED) = 'P<(^ SUBJ)>'
	  @(GLUE-VERB-SUBJ P).
	  
   VERB-SUBJ-OBJ(P) =
	  (^ PRED) = 'P<(^ SUBJ)(^ OBJ)>'
	  @(GLUE-VERB-SUBJ-OBJ P).

   VERB-SUBJ-COMP(P) =
	  (^ PRED) = 'P<(^ SUBJ)(^ COMP)>'
	  @(GLUE-VERB-SUBJ-COMP P).

   VERB-SUBJ-OBJ-OBJTH(P) =
	  (^ PRED) = 'P<(^ SUBJ)(^ OBJ)(^ OBJ-TH)>'
	  @(GLUE-VERB-SUBJ-OBJ-OBJTH P).

   COMP-TENSE = (^ COMP TENSE).

   VPAST = (^ VTYPE) = FIN
	  (^ TENSE) = PST.

----

GLUE   BASIC   TEMPLATES (1.0)

"Templates called by lexical entries"

   GLUE-PROPERNOUN(M) =
	  @(GLUE-REL0-MC ^ e M).

   GLUE-VERB-SUBJ(M) =
	  @(GLUE-REL1-MC (^ SUBJ) e ^ t M).

   GLUE-VERB-SUBJ-OBJ(M) =
	  @(GLUE-REL2-MC (^ SUBJ) e (^ OBJ) e ^ t M).

   GLUE-VERB-SUBJ-OBJ-OBJTH(M) =
	  @(GLUE-REL3-MC (^ SUBJ) e (^ OBJ) e (^ OBJ-TH) e ^ t M).

   GLUE-VERB-SUBJ-COMP(M) =
	  @(GLUE-REL2-MC (^ SUBJ) e (^ COMP) t ^ t M).

   GLUE-NOUN0(M) = "Meaning constructor for nouns associates the e->t-type meaning with the value of PRED"
	  @(GLUE-NOUN0-MC M).

   GLUE-ADJ0(M) = "Meaning constructors for prenominal adjectives provide the basic meaning and the meaning constructor that combines it with the noun"
          @(GLUE-REL1-MC (^ PRED) e ^ t M)
	  @(GLUE-ADJ-MODIFIER `[`/P`_`<e`,t`>`.`[`/Q`_`<e`,t`>`.`[`/x`_e`.and`(P`(x`)`,Q`(x`)`)`]`]`]).

   GLUE-QUANT-DET(M) = "Meaning constructor for quantificational determiners, with embedded implication and quantification over the scope structure"
	  @(GLUE-QUANT-MC M).

"Templates defining glue structures"
"Complex meaning expressions must be assembled from their parts using the built-in CONCAT template"

"Zero-arity predicates have a MEANING, a semantic structure, and a type."
"At this point (17 June 2020) zero-arity predicates are not associated with a type"
   GLUE-REL0-MC(R TY M) = 
	  @(GLUE-RESOURCE R %mc TY)
	  @(GLUE-MEANING %mc M)
	  %mc $ (R GLUE).

"Predicates with one or more arguments.  These are the simple cases,
which do not rely on attributes in the semantic structure.  Each
argument has a semantic structure and a type.  Note that when the
meaning expression is broken into parts as input to the CONCAT
template, special characters (including square brackets, the forward
slash, the comma, and the period) must be preceded by a backquote.
The underscore (and possibly other characters as well) must be
preceded by a backquote AND preceded and followed by a space."

"1-place predicates: 
   Representation without quotes for special characters: [/x_A1TY.M(x)]"
   GLUE-REL1-MC(A1 A1TY R TY M) = 
	  @(GLUE-RESOURCE R %mc TY)
	  @(GLUE-RESOURCE A1 (%mc ARG1) A1TY)
	  @(CONCAT `[ `/x `_ A1TY `. M `( x `) `] %MEANING)
	  @(GLUE-MEANING %mc %MEANING)
	  %mc $ (R GLUE).

"2-place predicates: 
   Representation without quotes for special characters: [/x_A1TY.[/y_A2TY.M(x,y)]]"
   GLUE-REL2-MC(A1 A1TY A2 A2TY R TY M) = 
	  @(GLUE-RESOURCE R %mc TY)
	  @(GLUE-RESOURCE A1 (%mc ARG1) A1TY) 
	  @(GLUE-RESOURCE A2 (%mc ARG2) A2TY) 
          @(CONCAT `[ `/ x `_ A1TY `. `[ `/ y `_ A2TY `. M `( x `, y `) `] `] %MEANING)
	  @(GLUE-MEANING %mc %MEANING)
	  %mc $ (R GLUE).

"3-place predicates:
   Representation without quotes for special characters: [/x_A1TY.[/y_A2TY.[/z_A3TY.M(x,y,z)]]]"
   GLUE-REL3-MC(A1 A1TY A2 A2TY A3 A3TY R TY M) = 
	  @(GLUE-RESOURCE R %mc TY)
	  @(GLUE-RESOURCE A1 (%mc ARG1) A1TY) 
	  @(GLUE-RESOURCE A2 (%mc ARG2) A2TY) 
	  @(GLUE-RESOURCE A3 (%mc ARG3) A3TY)
          @(CONCAT `[  `/ x `_ A1TY `.  `[  `/ y `_ A2TY `.  `[  `/ z `_ A3TY `. M `( x `, y `, z `)  `]  `]  `] %MEANING)  
	  @(GLUE-MEANING %mc %MEANING)
	  %mc $ (R GLUE).

"Special meaning constructor for nouns, with type
(^ PRED)_e -o (^ PRED)_t.  This template is also used for quantifiers
like 'everyone', so the local names in this template (%mcn and %MEANINGN)
must be different from the ones used in the quantifier template GLUE-QUANT-MC
(%mc and %MEANING)."
   GLUE-NOUN0-MC(M) = 
	  @(GLUE-RESOURCE (^ PRED) (%mcn ARG1) e) 
	  @(GLUE-RESOURCE (^ PRED) %mcn t)
	  @(CONCAT  `[  `/ x `_ e `. M `( x `)  `]  %MEANINGN)
  	  @(GLUE-MEANING %mcn %MEANINGN)
	  %mcn $ (^ GLUE).

"Special meaning constructor to combine basic adjective meanings
with the nouns they modify, with type
((^ PRED)_e -o ^_t) -o 
  ( ((ADJ $ ^) PRED)_e -o ((ADJ $ ^) PRED) -o 
     ((ADJ $ ^) PRED)_e -o ((ADJ $ ^) PRED))."
   GLUE-ADJ-MODIFIER(M) =
     @(GLUE-MODIFIER1 (^ PRED) e ^ t ((ADJ $ ^) PRED) e ((ADJ $ ^) PRED) t M).
 
"General template for one-place modifiers, e.g. adjectives"
  GLUE-MODIFIER1(MODARG MODARGTY MODRES MODRESTY HEADARG HEADARGTY HEADRES HEADRESTY M) =
         "Modifier"
         @(GLUE-RESOURCE MODARG (%mcm ARG1 ARG1) MODARGTY)
         @(GLUE-RESOURCE MODRES (%mcm ARG1) MODRESTY)
         "Head"
         @(GLUE-RESOURCE HEADARG (%mcm ARG2 ARG1) HEADARGTY)
         @(GLUE-RESOURCE HEADRES (%mcm ARG2) HEADRESTY)
	 "Result"
         @(GLUE-RESOURCE HEADARG (%mcm ARG3) HEADARGTY)
         @(GLUE-RESOURCE HEADRES %mcm HEADRESTY)
	 @(GLUE-MEANING %mcm M) 
	 %mcm $ (^ GLUE).
	 
"Special template for generalized quantifiers like 'every'.
   Representation without quotes for special characters: [/R_<e,t>.[/S_<e,t>.M(R,S)]]"
   GLUE-QUANT-MC(M) =
          "Restriction: (^ PRED)_e -o (^ PRED)_t"
	  @(GLUE-RESOURCE (^ PRED) (%mc ARG1 ARG1) e)
	  @(GLUE-RESOURCE (^ PRED) (%mc ARG1) t)
	  "Scope argument: ^_e -o ?SCOPE_t"
	  @(GLUE-RESOURCE ^ (%mc ARG2 ARG1) e)
	  @(GLUE-RESOURCE %scope (%mc ARG2) t)
	  "Result: ?SCOPE_t"
	  @(GLUE-RESOURCE %scope %mc t)
	  "introduce universal quantifier over scope with FORALL attribute"
 	  (%mc FORALL) = %scope
          "quantifier meaning"
   	  @(CONCAT `[ `/ R `_ `< e `, t `> `. `[ `/ S `_ `< e `, t `> `. M `( R `, S `) `] `] %MEANING)
 	  @(GLUE-MEANING %mc %MEANING)
 	  %mc $ (^ GLUE). 

"The base definitions.
GLUE-RESOURCE is a term: a linguistic resource R and a type TY.
GLUE-MEANING provides the meaning (left-hand side) of the full meaning constructor."

"The structure TypedRES has the value R for RESOURCE, and the value TY for TYPE."
   GLUE-RESOURCE(R TypedRES TY) = 
	  (TypedRES RESOURCE) = R
	  (TypedRES TYPE) = TY.

"The meaning M appears as the value of the attribute MEANING in the structure TypedRES."
   GLUE-MEANING(TypedRES M) =  
	  (TypedRES MEANING) = M.

----
